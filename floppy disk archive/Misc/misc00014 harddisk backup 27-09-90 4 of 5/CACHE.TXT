; Cache.TXT Copyright (C) 1989 Level 9 Computing.
;
; for Floor Map Loader/Cache.
;
; N.W.Austin 13/7/89
;
;-----

;CACHE.TXT: handle simple floormap cache
;Two entry points:
;   FCInitFloorCache
;   FCloadFloor

;-----

; draw flags are...
; dRemoveRedraw=65535  -1 Remove and redraw
; dInsert=0            insert
; dPlot=1              plot as sprite
; dInsertRedraw=2      insert and redraw
; dMarkPreload=3       mark to preload
; dSetProtect=4        set protection mark
; dUnsetProtect=5      unset protection mark

const
;Floor map directory must be 4*number of rooms (24 rooms = 220 bytes)
;Max floor map is 2290 bytes (=length)
 StartCache1=220
 StartCache2=2510 ;StartCache1+length

begin

code +
.FCInitFloorCache ;v1=cache size
code -
 CurrentCache=StartCache1
 Cache1=0
 Cache2=0

 v2=0 ;table
 MapNumber=48 ;'0'

.fci1
code +
 push v1
 push v2
code -
 gosub @LoadMap
code +
 pop v2
 pop v1
code -

 v3=CurrentCache
.fci2
code +
 &v4=FloorMap(v3) ;room number
code -
 if v4=0 then FCnextMap

 add v2,c4
 if v2>v1 then @FCtableFull
 sub v2,c4

code +
 &FloorMap(v2)=v4        ;room number
 add v2,c2
 &FloorMap(v2)=MapNumber ;file
 add v2,c2

 add v3,c2
 &v4=FloorMap(v3) ;length-2
code -
 add v3,v4
 goto fci2

.FCnextMap
 if MapNumber=57 then fci3 ;'9'
 if MapNumber=90 then fci4 ;'Z'
 add MapNumber,c1
 goto @fci1
.fci3
 MapNumber=65 ;'A'
 goto @fci1

.fci4
 add v2,c4
 if v2<v1 then fci5

.FCtableFull
 if v2>3 then fci5
 v2=4
.fci5
 sub v2,c4
code +
 &FloorMap(v2)=c0
 add v2,c2
 &FloorMap(v2)=c0

 CurrentCache=StartCache2
 &FloorMap(CurrentCache)=c0 ;Clear cache2
 CurrentCache=StartCache1
 &FloorMap(CurrentCache)=c0 ;Clear and select cache1
 MapNumber=0
 return

;-----

.FCnewRoomDv1
code -
 RoomNum=dv1 
 if RoomNum=0 then @FCnoRoom

 v1=0 ;cache table
.fcl1
code +
 &v2=FloorMap(v1)
code -
 if v2=0 then FCnoRoom
 if v2=RoomNum then fcl2
 add v1,c4
 goto fcl1

.fcl2
 add v1,c2
code +
 &MapNumber=FloorMap(v1)
code -

 if cache1=MapNumber then FCinCache1
 if cache2=MapNumber then FCinCache2

 if CurrentCache=StartCache1 then FCswitchCache
 CurrentCache=StartCache1
 Cache1=MapNumber
 goto fcl3
.FCswitchCache
 CurrentCache=StartCache2
 Cache2=MapNumber
.fcl3
 gosub @LoadMap
 gosub @FindRoom
code +
 return
code -

.FCinCache1
 CurrentCache=StartCache1
 gosub @FindRoom
code +
 return
code -

.FCinCache2
 CurrentCache=StartCache2

.FCnoRoom ;Set RoomNum=0

 gosub @FindRoom
code +
 return

;-----
